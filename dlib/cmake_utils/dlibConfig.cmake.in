@PACKAGE_INIT@

include(CMakeFindDependencyMacro)
list(APPEND CMAKE_MODULE_PATH "${CMAKE_CURRENT_LIST_DIR}")

find_dependency(Threads)

if ( NOT @DLIB_NO_GUI_SUPPORT@ )
    find_dependency(X11 REQUIRED)
endif()

if ( @DLIB_GIF_SUPPORT@ )
    find_dependency(GIF REQUIRED)
endif()

if ( @DLIB_JPEG_SUPPORT@ AND @JPEG_FOUND@ )
    find_dependency(JPEG REQUIRED)
endif()

if ( @DLIB_PNG_SUPPORT@ AND @PNG_FOUND@ )
    find_dependency(PNG REQUIRED)
endif()

if ( @DLIB_WEBP_SUPPORT@ )
    find_dependency(WEBP REQUIRED)
endif()

if ( @DLIB_LINK_WITH_SQLITE3@ )
    find_dependency(SQLite3 REQUIRED)
endif()

if ( @DLIB_USE_FFMPEG@ )
    find_dependency(FFMPEG REQUIRED)
endif()

if ( @DLIB_USE_CUDA@ )
    find_dependency(CUDAToolkit REQUIRED)
    find_dependency(CUDNN REQUIRED)
endif()

if ( @DLIB_USE_MKL_SEQUENTIAL@ OR @DLIB_USE_MKL_WITH_TBB@ OR @DLIB_USE_MKL_WITH_GNU@ )
    find_dependency(MKL REQUIRED)
endif()

if ( @DLIB_USE_BLAS@ )
    find_dependency(CBLAS REQUIRED)
endif()

include("${CMAKE_CURRENT_LIST_DIR}/dlib.cmake")

check_required_components(dlib)