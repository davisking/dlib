include(FindPackageHandleStandardArgs)

if (WIN32 AND NOT CYGWIN)
    find_library(GDI32_LIBRARY NAMES gdi32)
    find_library(IMM32_LIBRARY NAMES imm32)
    find_library(COMM_LIBRARY  NAMES comctl32)
    mark_as_advanced(GDI32_LIBRARY IMM32_LIBRARY COMM_LIBRARY)

    if (GDI32_LIBRARY AND IMM32_LIBRARY AND COMM_LIBRARY)
        set(GUI_LIBRARIES ${COMM_LIBRARY} ${GDI32_LIBRARY} ${IMM32_LIBRARY})
        add_library(gui::gdi32 UNKNOWN IMPORTED)
        set_target_properties(gui::gdi32 PROPERTIES IMPORTED_LOCATION ${GDI32_LIBRARY})
        add_library(gui::imm32 UNKNOWN IMPORTED)
        set_target_properties(gui::imm32 PROPERTIES IMPORTED_LOCATION ${IMM32_LIBRARY})
        add_library(gui::comm UNKNOWN IMPORTED)
        set_target_properties(gui::comm PROPERTIES IMPORTED_LOCATION ${COMM_LIBRARY})
        add_library(gui::gui INTERFACE IMPORTED)
        target_link_libraries(gui::gui INTERFACE gui::comm gui::gdi32 gui::imm32)
    endif()
else()
    find_package(X11)

    if (X11_FOUND)
        set(GUI_LIBRARIES ${X11_LIBRARIES})
        add_library(gui::gui ALIAS X11::X11)
    elseif(APPLE)
        message(" *****************************************************************************")
        message(" *** DLIB GUI SUPPORT DISABLED BECAUSE X11 DEVELOPMENT LIBRARIES NOT FOUND ***")
        message(" *** Make sure XQuartz is installed if you want GUI support.               ***")
        message(" *** You can download XQuartz from: https://www.xquartz.org/               ***")
        message(" *****************************************************************************")
    else()
        message(" *****************************************************************************")
        message(" *** DLIB GUI SUPPORT DISABLED BECAUSE X11 DEVELOPMENT LIBRARIES NOT FOUND ***")
        message(" *** Make sure libx11-dev is installed if you want GUI support.            ***")
        message(" *** On Ubuntu run: sudo apt-get install libx11-dev                        ***")
        message(" *****************************************************************************")
    endif()
endif()

find_package_handle_standard_args(GUI DEFAULT_MSG GUI_LIBRARIES)